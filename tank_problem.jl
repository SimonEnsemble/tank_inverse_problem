### A Pluto.jl notebook ###
# v0.19.42

using Markdown
using InteractiveUtils

# This Pluto notebook uses @bind for interactivity. When running this notebook outside of Pluto, the following 'mock version' of @bind gives bound variables a default value (instead of an error).
macro bind(def, element)
    quote
        local iv = try Base.loaded_modules[Base.PkgId(Base.UUID("6e696c72-6542-2067-7265-42206c756150"), "AbstractPlutoDingetjes")].Bonds.initial_value catch; b -> missing; end
        local el = $(esc(element))
        global $(esc(def)) = Core.applicable(Base.get, el) ? Base.get(el) : iv(el)
        el
    end
end

# â•”â•â•¡ faf59350-8d67-11ee-0bdd-2510e986118b
begin
    import Pkg; Pkg.activate()
    using CSV, Interpolations, DataFrames, CairoMakie, DifferentialEquations, Turing, StatsBase, PlutoUI, Distributions, Optim, Dierckx
end

# â•”â•â•¡ 4391f124-cbef-46e5-8462-e4e5126f5b38
begin
	# cool plot theme
	import AlgebraOfGraphics
	
	AlgebraOfGraphics.set_aog_theme!(
		fonts=[
			AlgebraOfGraphics.firasans("Light"), 
			AlgebraOfGraphics.firasans("Light")
		]
	)
	
	resolution = (0.9 * 500, 0.9 * 380)
	
	update_theme!(
		fontsize=20, 
		linewidth=4,
		markersize=14,
		titlefont=AlgebraOfGraphics.firasans("Light"),
		size=resolution
	)

	colors = Dict(zip(
		["data", "model", "distn", "other"], 
		AlgebraOfGraphics.wongcolors()[1:4])
	)
end

# â•”â•â•¡ 245836a9-6b44-4639-9209-e7ad9035e293
TableOfContents()

# â•”â•â•¡ 7752316d-9dd0-4403-aa08-22c977ff3727
md"""
# tank geometry and measurements

we characterize the area of the liquid holding tank, as a function of height $h$, from a helicopter view. the cross-sectional area we model as a [rounded rectangle](https://mathworld.wolfram.com/RoundedRectangle.html). see notes in `tank_geometry/cory_measurements.txt`.
"""

# â•”â•â•¡ c976bc08-97b2-45c0-b1ff-1819e7290a68
struct TankMeasurements
	# top, bottom areas (cmÂ²)
	A_t::Float64
	A_b::Float64
	
	# height (cm)
	H::Float64

	# radius of hole
	r_hole::Float64

	# height of hole
    h_hole::Float64
end

# â•”â•â•¡ 20fa4266-be80-4d8e-b1d0-155a40a1241f
begin
	# height of tank (along slant) [cm]
	Hâ˜… = 28.6

	# measurements of top cross-section
	L_t = 14.6 #cm
	W_t = 9.0  # cm
	p_t = 44.3 # perimeter, cm
	
	# measurements of bottom cross-section
	L_b = 13.4 # cm
	W_b = 7.8  # cm
	p_b = 40.1 # perimeter, cm
	
	# height of tank (from perpendicular) [cm]
	local Î´ = (L_t - L_b) / 2 # overhang
	H = sqrt(Hâ˜… ^ 2 - Î´ ^ 2)

	# solve for the r consistent with a rounded rectangle [cm]
	#  https://mathworld.wolfram.com/RoundedRectangle.html
	local r(p, L, W) = (p / 2 - (L + W)) / (Ï€ - 4)
	r_b = r(p_b, L_b, W_b)
	r_t = r(p_b, L_b, W_b)
	@show r_b, r_t

	# finally, calculate areas.
	my_A(L, W, r) = (L - 2 * r) * (W - 2 * r) + 2 * r * (L + W - 4 * r) + Ï€ * r ^ 2
	A_b = my_A(L_b, W_b, r_b)
	A_t = my_A(L_t, W_t, r_t)
	@show A_b, A_t

	# hole in tank (radius, area, height)
	r_hole = 5 / 64 * 2.54 / 2 # cm
    a_hole = Ï€ * r_hole ^ 2    # cmÂ²
    h_hole = 0.9 # cm

	tank_measurements = TankMeasurements(
		A_t, A_b, H, r_hole, h_hole
	)
end

# â•”â•â•¡ 48d7273e-a48b-49fd-991b-6e29f64a0760
"""
cross-sectional area of water from helicopter view, as a function of liquid level, h.
"""
function A_of_h(h::Float64, tm::TankMeasurements)
	# check for over/underflow
	h < 0.0  ? error("tank underflow!") : nothing
	h > tm.H ? error("tank overflow!")  : nothing
	# linearly interpolate top and bottom areas
	return h / tm.H * tm.A_t + (1 - h / tm.H) * tm.A_b
end

# â•”â•â•¡ 9a7e5903-69be-4e0a-8514-3e05feedfed5
begin
	local fig = Figure()
	local ax = Axis(
		fig[1, 1], 
		xlabel="water level, h [cm]", 
		ylabel="cross-sectional area, A(h) [cmÂ²]"
	)
	lines!(range(0, H), 
		[A_of_h(háµ¢, tank_measurements) for háµ¢ in range(0, tank_measurements.H)]
	)
	vlines!(tank_measurements.H, linestyle=:dash, color="gray")
	xlims!(0, nothing)
	ylims!(0, nothing)
	fig
end

# â•”â•â•¡ 418525b7-c358-41da-b865-5df3feb15855
md"
# calibration of liquid level sensor

read in data characterizing the calibration curve of the liquid level sensor.
"

# â•”â•â•¡ a95e371e-9319-4c7e-b5d9-4c4a50d12cd7
begin
	calibration_data = CSV.read("calibration_curve.csv", DataFrame)
	sort!(calibration_data, "level sensor reading")
	rename!(calibration_data, "h [cm]" => "hâ˜… [cm]")
end

# â•”â•â•¡ 6d48d04b-0786-4d15-a07b-8941805a5b09
md"we correct for the slant, though slight, in the tank, which makes the level strip slanted. this function maps the slanted hâ˜… to the height h perpendicular to the ground.
"

# â•”â•â•¡ 9af216b7-4bf2-42fb-bd95-5b2040d019a7
hâ˜…_to_h(hâ˜…) = H * hâ˜… / Hâ˜…

# â•”â•â•¡ 6ebe0cb0-ba35-411c-9a7a-a8b6eecf326f
md"compute the true liquid level."

# â•”â•â•¡ 385442da-f101-4ddf-8293-46d71d6a48fc
begin
	calibration_data[:, "h [cm]"] = hâ˜…_to_h.(calibration_data[:, "hâ˜… [cm]"])
	calibration_data
end

# â•”â•â•¡ ef43e50a-5af8-4733-88a4-cd159d173034
md"fit spline to calibration data to construct calibration curve."

# â•”â•â•¡ 9dabad13-cfa4-4e06-950d-f7c7d96c1147
begin
	# map liquid level sensor reading to liquid level h.
	level_sensor_to_h = Spline1D(
		calibration_data[:, "level sensor reading"],
		calibration_data[:, "h [cm]"];
		k=2, s=4.0, bc="error"
	)
end

# â•”â•â•¡ e040094c-7511-4831-b94a-1c1185868202
md"viz calibration data as well as interpolator."

# â•”â•â•¡ 23ee0e85-a84b-4b63-b432-5526559efcee
begin
	local fig = Figure()
	local ax = Axis(
		fig[1, 1], 
		xlabel="level sensor reading", 
		ylabel="liquid level, h [cm]"
	)
	scatter!(
		calibration_data[:, "level sensor reading"], 
		calibration_data[:, "h [cm]"]
	)
	lsr = range(
		minimum(calibration_data[:, "level sensor reading"]),
		maximum(calibration_data[:, "level sensor reading"]),
		length=100
	)
	lines!(lsr, level_sensor_to_h.(lsr), color=Cycled(2))
	fig
end

# â•”â•â•¡ 078c01f7-e47e-4af0-be1c-ac4527b735fd
md"
# time series data processing
"

# â•”â•â•¡ 8b00d2b3-9182-42ab-8393-91707b813f60
function read_h_time_series(file::String)
	#=
	read in file
	=#
	data = CSV.read(file, DataFrame)
	rename!(data, 
		" liquid_level_reading" => "liquid_level_reading",
		"Time [s]" => "t [s]"
	)

	#=
	find starting point, t = 0, as when max water level occurs.
	=#
	# index of highest water level
	id_start = argmax(data[:, "liquid_level_reading"])
	# remove data before that
	data = data[id_start:end, :]
	# shift time
	data[:, "t [s]"] = data[:, "t [s]"] .- data[1, "t [s]"]

	#=
	apply calibration curve to get h
	=#
	data[:, "h [cm]"] = level_sensor_to_h.(data[:, "liquid_level_reading"])
	
	return select(data, ["t [s]", "h [cm]"])
end

# â•”â•â•¡ 7899f488-9c48-466f-857d-f5a31b5820ab
md"""
## read in train/test time series data
"""

# â•”â•â•¡ 96f26378-846c-4964-935c-0372e2e86e91
md"time series data for two experiments without any object in the tank"

# â•”â•â•¡ 2ddf387c-5a61-4490-9746-96e1589c7a74
train_experiment = "no_obs_4_18_2.csv"

# â•”â•â•¡ b2228d5c-16b4-4fee-b9b6-1112d7cf391c
test_experiment  = "no_obs_4_18_3.csv"

# â•”â•â•¡ 661feb84-339c-4bbe-a8a5-65de74ed58c8
all_train_data = read_h_time_series(train_experiment)

# â•”â•â•¡ 1e8a535e-25ea-490b-b545-e532c4fbc0f3
all_test_data = read_h_time_series(test_experiment)

# â•”â•â•¡ a0849611-23b3-4a91-a054-f390bc6c9f0a
md"""
## visualize experimental data
"""

# â•”â•â•¡ 33f889d7-e875-40d8-9d6d-cc87b0fbaf22
function viz_data(data::DataFrame)
	fig = Figure()
	ax = Axis(
		fig[1, 1], 
		xlabel="time, t [s]", 
		ylabel="water level, h [cm]"
	)
	scatter!(
		data[:, "t [s]"], 
		data[:, "h [cm]"],
		label="experiment",
		color=colors["data"]
	)
	xlims!(0, nothing)
	ylims!(0, nothing)
	fig
end

# â•”â•â•¡ 0e4b3c36-2f09-405e-912b-22c893cd1715
viz_data(all_train_data)

# â•”â•â•¡ ddd8f749-3126-4563-8177-4941b6b0447b
viz_data(all_test_data)

# â•”â•â•¡ 710ddf42-2397-4d96-9a61-ff5c600ccd43
md"## downsampling"

# â•”â•â•¡ 46084a31-e591-42f2-b8e6-02183ddfc6ac
@bind n_data_sample Select([20, 50, 100], default=20)

# â•”â•â•¡ f21dc58e-d4e8-4314-b5dd-abbcb29efe86
function downsample(data::DataFrame, n::Int)
	# filter out sensor data that is below 508
	# this is the liquid level where the jet shooting outside the tank
	# dies, and it just runs down the tank (Toricelli's law invalid)
	data = filter("h [cm]" => (x -> x >= level_sensor_to_h(508)), data)
	
	ids = trunc.(Int, collect(range(1, nrow(data), length=n)))
	return data[ids, :]
end

# â•”â•â•¡ 14b713b9-70c6-4506-a7b3-c67d021f8fce
train_data = downsample(all_train_data, n_data_sample)

# â•”â•â•¡ 86b34265-6f2d-48d7-95ea-d10c8ae29ea9
viz_data(train_data)

# â•”â•â•¡ 6bbb7a2c-464f-4e92-ab15-66745bac03ef
test_data = downsample(all_test_data, n_data_sample)

# â•”â•â•¡ bcf21d4b-cc76-4b9e-8cdb-7a17eb2af605
viz_data(test_data)

# â•”â•â•¡ e379461f-8896-4e2a-a71b-1871a8a37eb5
md"""
# dynamic model of the liquid level

```math
A(h)\frac{dh}{dt} = - c\pi r_{\rm hole}^2 \sqrt{2g [h(t)-h_{\rm hole}]}
```
"""

# â•”â•â•¡ 7b7baa41-0185-4ed6-8fae-3a44e9912016
g = 980.665 # cm/sÂ²

# â•”â•â•¡ f2f236f4-59f3-4c05-811d-078cd04ddd79
md"""
## dynamic model 
"""

# â•”â•â•¡ c6a263eb-cb45-4ee7-9c02-549c89298652
function f(h, params, t)
	if h < params.h_hole
		return 0.0
	end
	return - Ï€ * params.r_hole ^ 2 * params.c * 
		sqrt(2 * g * (h .- params.h_hole)) / params.A_of_h(h)
end

# â•”â•â•¡ 05ed4187-a01a-4a16-a0e7-b3867d252578
function simulate(hâ‚€, params::NamedTuple, tf::Float64, callback=nothing)
	prob = ODEProblem(f, hâ‚€, (0.0, tf), params)
	h_of_t = solve(
		prob, Tsit5(), saveat=0.5, 
		reltol=1e-6, abstol=1e-6, 
		callback=callback
	)
	return h_of_t
end

# â•”â•â•¡ 6f7d4335-d9a6-4896-9d69-bfc1c2c1c3d0
md"""
## minimize loss (classical approach) to identify $c$
"""

# â•”â•â•¡ 66815e8e-09d9-4b43-9f45-9379b3d34f78
function loss(data::DataFrame, c::Float64, tm::TankMeasurements)	
	params = (
		# area of the hole
		r_hole = tm.r_hole,
		# fudge factor
		c = c, 
		# height of the hole
		h_hole = tm.h_hole,
		# area as a function of h
		A_of_h = h -> A_of_h(h, tm)
	)

	h_of_t = simulate(
		data[1, "h [cm]"], # hâ‚€
		params,
		data[end, "t [s]"] # end of time
	)
	
	cost = 0.0
	for i in 1:nrow(data)
		táµ¢ = data[i, "t [s]"]
		cost += (data[i, "h [cm]"] - h_of_t(táµ¢)) ^ 2
	end
	
	return cost
end

# â•”â•â•¡ 5e79d8e1-429c-414f-b3a6-8cf4b93d1336
md"maximum likelihood estimate of $c$"

# â•”â•â•¡ 0b75c073-f167-4553-b746-539a14cfcf25
loss(train_data, 0.4, tank_measurements)

# â•”â•â•¡ 4ed31219-9ce0-4f1b-8152-0002e64649ad
function compute_mle(data::DataFrame, tm::TankMeasurements)
	res = optimize(c -> loss(data, c, tm), 0.2, 0.9)
	return Optim.minimizer(res)
end

# â•”â•â•¡ 0763f1f3-dfee-4d1f-a934-bf387f9c80ff
c_opt = compute_mle(train_data, tank_measurements)

# â•”â•â•¡ c57c808a-297c-4887-bf20-5ad0207d055e
params = (
	# area of the hole
	r_hole = tank_measurements.r_hole,
	# fudge factor
	c = c_opt,
	# height of the hole
	h_hole = tank_measurements.h_hole,
	# area as a function of h
	A_of_h = h -> A_of_h(h, tank_measurements)
)

# â•”â•â•¡ d3307918-1fdb-4f87-bb92-67330d22e58b
sim_train_data = DataFrame(
	simulate(
		train_data[1, "h [cm]"], # hâ‚€
		params,
		1.1 * train_data[end, "t [s]"] # end of time
	)
)

# â•”â•â•¡ d25cda2f-6ec6-4c93-8860-f5ce9c3ee629
md"""
## visualize dynamic model (classically fit)
"""

# â•”â•â•¡ 444f6d74-273e-486d-905a-1443ec0e98df
function viz_sim_fit(data::DataFrame, sim_data::DataFrame; 
		             savename::Union{Nothing, String}=nothing)
	 fig = Figure()
	 ax = Axis(
		fig[1, 1], 
		xlabel="time, t [s]", 
		ylabel="liquid level, h[cm]"
	)
	
	scatter!(
		data[:, "t [s]"], 
		data[:, "h [cm]"], 
		label="experiment"
	)
	
	lines!(sim_data[:, "timestamp"], sim_data[:, "value"], 
		label="model", color=Cycled(2)
	)

	axislegend()
	if ! isnothing(savename)
		save("$savename.pdf", fig)
	end
	fig
end

# â•”â•â•¡ 8cfdc784-4060-48b8-8d1a-3b8d11f7a9a7
viz_sim_fit(train_data, sim_train_data)

# â•”â•â•¡ a1a10e2f-1b78-4b93-9295-7c0055e32692
md"""
# Bayesian inference for model parameters
"""

# â•”â•â•¡ 58eff13c-44b5-4f19-8a42-cf9907ac9515
@bind n_MC_sample Select([10, 50, 100, 250], default=10)

# â•”â•â•¡ 8a21fa0f-d3c3-4aa2-8b8b-74001d921c4a
md"""
## infer model parameters for object-free experiment (training)
"""

# â•”â•â•¡ 8f5b8859-6b8c-4f2a-af3a-b13c2d33fe2a
@model function forward_model(data::DataFrame, tm::TankMeasurements)
	#=
	prior distributions
	=#
	# defines variance for measuring length with measuring tape
	Ïƒ_â„“ ~ Uniform(0.0, 0.5) # cm
	
	# bottom, top tank area measurements
	# std of product of two Guassians
	#   https://ccrma.stanford.edu/~jos/sasp/Product_Two_Gaussian_PDFs.html
	A_b ~ Normal(tm.A_b, Ïƒ_â„“ ^ 2 / 2) # cmÂ²
	A_t ~ Normal(tm.A_t, Ïƒ_â„“ ^ 2 / 2) # cmÂ²

	# height of tank
	H ~ Truncated(
		Normal(tm.H, Ïƒ_â„“),
		tm.H - Ïƒ_â„“, tm.H + Ïƒ_â„“
	) # cm

	# radius of the hole. std 2%
	r_hole ~ Truncated(
		Normal(tm.r_hole, 0.02 * tank_measurements.r_hole),
		0.9 * tm.r_hole, 1.1 * tm.r_hole
	) # cm

	# discharge coefficient. Wikipedia says 0.65 for water.
	c ~ Truncated(Normal(0.65, 0.1), 0.1, 1.0) # unitless

	# height of the hole
	h_hole ~ Truncated(
		Normal(tm.h_hole, Ïƒ_â„“),
		tm.h_hole - Ïƒ_â„“, tm.h_hole + Ïƒ_â„“
	) # cm
	
	# defines variance of liquid level sensor
	#   (treated as an unknown and inferred)
	Ïƒ ~ Uniform(0.0, 1.0) # cm

	# initial liquid level
	hâ‚€_obs = data[1, "h [cm]"] # cm
	hâ‚€ ~ Truncated(
		Normal(hâ‚€_obs, Ïƒ),
		0.0, H
	)

	#=
	set up dynamic model for h(t)
	=#
	function my_A_of_h(h)
		if h < 0.0 error("h < 0") end
		if h > H error("h > H") end
		return h / H * A_t + (1 - h / H) * A_b
	end
	
	# parameters for ODE solver
	params = (
			  r_hole=r_hole,
			  c=c,
			  h_hole=h_hole,
			  A_of_h=my_A_of_h
			)
	
	# set up and solve ODE
	h_of_t = simulate(hâ‚€, params, 1.1 * data[end, "t [s]"])
	
	#=
	code up likelihood
	=#
	for i in 2:nrow(data) # start at 2 b/c IC handled with informative prior
		táµ¢ = data[i, "t [s]"]
		hÌ‚áµ¢ = h_of_t(táµ¢, continuity=:right)[1]
		data[i, "h [cm]"] ~ Normal(hÌ‚áµ¢, Ïƒ)
	end

	return nothing
end

# â•”â•â•¡ 8082559e-a5b0-41a8-b8ed-aec3b09e5b2b
begin
	train_model = forward_model(train_data, tank_measurements)
	
	train_posterior = DataFrame(
		sample(
			train_model, 
			NUTS(0.65), MCMCSerial(), n_MC_sample, 3; progress=true
		)
	)
end

# â•”â•â•¡ 2ee1ca40-141f-40ad-b4c1-a2e025f69f95
md"make sure never $h_0>H$."

# â•”â•â•¡ c2d877b5-d309-4868-925d-dab8d7d23403
@assert all(train_posterior[:, "H"] .> train_posterior[:, "hâ‚€"])

# â•”â•â•¡ c239deed-8291-45aa-95cf-94df26e0136d
md"""
## visualize posterior distribution of model parameters
"""

# â•”â•â•¡ ccb1f005-567d-47f8-bec1-8db268d878ec
inferred_params = ["Ïƒ_â„“", "A_b", "A_t", "H", "r_hole", "c", "h_hole", "Ïƒ", "hâ‚€"]

# â•”â•â•¡ 5bb0b72a-8c77-4fcb-bbde-d144986d9c1e
function viz_posterior(posterior::DataFrame, params::Vector{String},
			           tm::TankMeasurements, hâ‚€_obs::Float64
)
	params_to_title = Dict(
						"A_b" => "A, tank bottom", 
						"A_t" => "A, tank top",
						"r_hole" => "hole radius",
						"c" => "discharge coefficient",
						"h_hole" => "hole height",
						"hâ‚€" => "hâ‚€",
						"Ïƒ_â„“" => "std length measurement",
						"H" => "tank height", 
						"Ïƒ" => "std level sensor"
	)
	
	fig = Figure(size=(600, 600))
	i, j = 1, 1 # row, col
	 
	for p in params
		# vizualize the distribution
		ax = Axis(fig[i, j])
		hist!(ax, posterior[:, p], color=Cycled(1))

		# plot equal-tailed 80% interval
		lo, hi = quantile(posterior[:, p], [0.1, 0.9])
		lines!(ax, [lo, hi], [0, 0], linewidth=5, color=Cycled(2))
		
		ax.xlabel = params_to_title[p]

		if j == 1
			ax.ylabel = "density"
		end
		
		if ! (p in ["hâ‚€", "c", "Ïƒ_â„“", "Ïƒ", "dp"])
			p_obs = getfield(tm, Symbol(p))
			# plot measured value
			vlines!(ax, p_obs, linestyle=:dash, color=Cycled(3), 
					label="measurement")
		elseif p == "hâ‚€"
			vlines!(ax, hâ‚€_obs, linestyle=:dash, color=Cycled(3), label="true value")
		end
		
		if j == 3
			i += 1
			j = 1
		else
			j += 1
		end
	end
	
	return fig
end

# â•”â•â•¡ ded5b462-06dd-43a4-93b0-c52ad87174eb
viz_posterior(train_posterior, inferred_params, tank_measurements, train_data[1, "h [cm]"])

# â•”â•â•¡ 86b56683-c80e-4c0f-8b03-a4869860d04f
md"## posterior predictive check"

# â•”â•â•¡ 2ab35999-3615-4f5c-8d89-36d77802fe9b
function viz_fit(posterior::DataFrame, data::DataFrame; 
				savename::Union{String, Nothing}=nothing, n_sample::Int=50
)
	fig = Figure(resolution=(700, 500))
	ax = Axis(
		fig[1, 1], 
		xlabel="time, t [s]", 
		ylabel="liquid level, h [cm]"
	)
	
	ts = range(0, maximum(data[:, "t [s]"]), length=500)
	tspan = (0.0,  maximum(ts) * 1.05)
	

	# sample posterior models
	for i in sample(1:nrow(posterior), n_sample)
		params = (
			  r_hole=posterior[i, "r_hole"],
			  c=posterior[i, "c"],
			  h_hole=posterior[i, "h_hole"],
			  A_of_h=h -> h / posterior[i, "H"] * posterior[i, "A_t"] + 
			  	    (1 - h / posterior[i, "H"]) * posterior[i, "A_b"]
			)

		# set up, solve ODE
		sim_data = DataFrame(
			simulate(posterior[i, "hâ‚€"], params, 1.05 * maximum(ts))
		)
			
		
		lines!(
			sim_data[:, "timestamp"], sim_data[:, "value"], 
			label="model", color=(colors["model"], 0.1)
		)
	end	
	scatter!(
		data[:, "t [s]"], 
		data[:, "h [cm]"],
		label="experiment",
		color=colors["data"]
	)
	axislegend(unique=true)
	ylims!(0, nothing)
	xlims!(0, nothing)
	if savename!=nothing
		save( "$savename.pdf", fig)
	end
	return fig
end

# â•”â•â•¡ 2a01b228-f281-46c4-9764-fac6cc1b4217
viz_fit(train_posterior, train_data)

# â•”â•â•¡ a5ae695b-bfc0-4425-9b64-bbeeba7da015
md"""
## validate posterior with test data set
"""

# â•”â•â•¡ eaf470e9-2898-41d5-a6d5-4cd846e9c0de
function viz_test(posterior::DataFrame, test_data::DataFrame;
				 savename::Union{String, Nothing}=nothing, n_sample::Int=100
)
	fig = Figure(size=(600, 600))
	
	ax_stopping = Axis(
		fig[1, 1], 
		ylabel="# samples", 
		xticks=([],[]),
		height=100
	)
	ax = Axis(
		fig[2, 1], 
		xlabel="time, t [s]", 
		ylabel="liquid level, h [cm]",
	)

	# sample from the train posterior
	emptying_time = Float64[]
	for i in sample(1:nrow(posterior), n_sample)
		# check for first instance when the liquid level
		#  is the same as the height of the hole in the base
		condition(h, t, integrator) = h[1] - posterior[i, "h_hole"]
		
		# retrive the emptying time [t] when h(t) = h_hole
		function affect!(integrator)
			push!(emptying_time, integrator.t)
		end
		cb = ContinuousCallback(condition, affect!)

		params = (
			  r_hole=posterior[i, "r_hole"],
			  c=posterior[i, "c"],
			  h_hole=posterior[i, "h_hole"],
			  A_of_h=h -> h > posterior[i, "H"] ? 
			  	error("h > H") : 
			   	h / posterior[i, "H"] * posterior[i, "A_t"] + 
			  	    (1 - h / posterior[i, "H"]) * posterior[i, "A_b"]
		)

		# sample an initial condtion
		hâ‚€_obs = test_data[1, "h [cm]"]
		hâ‚€_distn = Truncated(
			Normal(hâ‚€_obs, posterior[i, "Ïƒ"]),
			0.0, posterior[i, "H"]
		)
		hâ‚€ = rand(hâ‚€_distn)

		# simulate trajectory
		sim_data = DataFrame(
			simulate(hâ‚€, params, 1.25 * test_data[end, "t [s]"], cb)
		)
			
		# plot trajectories
		lines!(ax, sim_data[:, "timestamp"], sim_data[:, "value"], 
			label="model", color=(colors["model"], 0.1))
		hlines!(ax, posterior[i, "h_hole"], color=("gray", 0.1), linestyle=:dash)
	end

	# plot dist'n of emptying times
	hist!(ax_stopping, emptying_time, color=Cycled(3))

	scatter!(
		ax,
		test_data[:, "t [s]"], 
		test_data[:, "h [cm]"],
		label="experiment",
		color=colors["data"]
		)
	xlims!(0, maximum(emptying_time))
	ylims!(ax, 0, nothing)

	linkxaxes!(ax, ax_stopping)
	axislegend(ax, unique=true)
	
	if isnothing(savename)
		save("$savename.pdf", fig)
	end
	
	return fig 
end	

# â•”â•â•¡ a3ba0c9d-5f81-4023-9ce0-ff29536aa968
viz_test(train_posterior, test_data, savename="test")

# â•”â•â•¡ 9533c662-80af-4dd4-bf25-02e894867360
md"""
# Bayesian inference of object shape

## read experimental data
"""

# â•”â•â•¡ b06a1c07-6250-4324-8802-010e5d847edb
begin
	data_w_object_filenames = ["obs_4_18_1.csv", "obs_4_18_2.csv"]
	@bind data_w_object_filename Select(data_w_object_filenames)
end

# â•”â•â•¡ 8b6d766a-8f7b-4b9a-9a15-0f7375087120
all_data_w_object = read_h_time_series(data_w_object_filename)

# â•”â•â•¡ 807222ba-5ff8-4f33-a9a0-7c69b1dccf52
data_w_object = downsample(all_data_w_object, n_data_sample)

# â•”â•â•¡ 16158266-36ed-44c3-a418-0c454955ce78
begin
	function viz_free_vs_occupied(data_w_object::DataFrame, data::DataFrame)
		fig = Figure()
		ax = Axis(
			fig[1, 1], 
			xlabel="time, t [s]", 
			ylabel="water level, h [cm]"
		)
		scatter!(
			data[:, "t [s]"], 
			data[:, "h [cm]"],
			label="no object",
			color=colors["data"]
		)
		scatter!(
			data_w_object[:, "t [s]"], 
			data_w_object[:, "h [cm]"],
			label="with object",
			color=colors["other"]
		)
		axislegend()
		ylims!(0, nothing)
		xlims!(0, nothing)
		save("h_of_t_with_without_object.pdf", fig)
		fig
	end
	
	viz_free_vs_occupied(data_w_object, train_data)
end

# â•”â•â•¡ 580de17a-625d-420e-974c-86766197025e
md"## measured area of object

a ground truth"

# â•”â•â•¡ cb59f55b-c748-4a94-b344-e50a8fa7c690
begin
	object_true_area = CSV.read("obstacle_area.csv", DataFrame)
	rename!(object_true_area, "area " => "area [cmÂ²]")
	select!(object_true_area, ["h [cm]", "area [cmÂ²]"])
end

# â•”â•â•¡ 89cced40-f24e-499e-8bfd-19c3964f689b
A_of_object = Spline1D(object_true_area[:, "h [cm]"], 
					  object_true_area[:, "area [cmÂ²]"]; k=3, s=10.0, bc="zero")

# â•”â•â•¡ b9515b3a-b254-49ae-8c2c-b8ce7ced4d3a
begin
	local fig = Figure()
	local ax = Axis(
		fig[1, 1], 
		xlabel="water level, h [cm]", 
		ylabel="cross-sectional area, Aâ‚’(h) [cmÂ²]"
	)
	
	h_range = range(0.0, object_true_area[end, "h [cm]"], length=100)
	
	lines!(h_range, A_of_object.(h_range), color=Cycled(2))
	scatter!(object_true_area[:, "h [cm]"], object_true_area[:, "area [cmÂ²]"])
	ylims!(0, nothing)
	xlims!(0, nothing)
	fig
end

# â•”â•â•¡ a8861082-2214-45f1-bc49-733efe74c949
md"## simulate model with knowledge of true area

in practice, couldn't do this. just a check to see if this has any hope...
"

# â•”â•â•¡ b59fa654-6946-4687-b14b-c2ef1f766f5c
object_params = (
		# area of the hole
		r_hole = tank_measurements.r_hole,
		# fudge factor
		c = c_opt,
		# height of the hole
		h_hole = tank_measurements.h_hole,
		# area as a function of h"
		A_of_h = h -> A_of_h(h, tank_measurements) - A_of_object(h)
	)

# â•”â•â•¡ b12963ae-bf7d-4ef7-b1a8-e2d1e24f9b4b
sim_object_data = DataFrame(
	simulate(
		data_w_object[1, "h [cm]"], object_params, data_w_object[end, "t [s]"]
	)
)

# â•”â•â•¡ cfbe753d-85a8-445f-9eda-14a376d7e0c6
viz_sim_fit(data_w_object, sim_object_data)

# â•”â•â•¡ c53edeef-324a-418f-907d-aaf557cb8d24
md"## classical method


```math
[A(h)-A_o(h)]\frac{dh}{dt} = - c\pi r_{\rm hole}^2 \sqrt{2g [h(t)-h_{\rm hole}]}
```

thus
```math
A_o(h) = A(h) + \dfrac{c\pi r_{\rm hole}^2 \sqrt{2g [h(t)-h_{\rm hole}]}}{dh/dt}
```
(don't be alarmed at the sign, dh/dt < 0)

ğŸ’¡ fit splines to data, differentiate splines to get dh/dt.
"

# â•”â•â•¡ 4831a3be-35d3-420c-8463-bb14a597cc6a
begin
	# fitting to data past drainage introduces spurious curvature
	data_w_object_spline_fit = filter(
		row -> row["h [cm]"] >= tank_measurements.h_hole + 1.0, data_w_object
	)
	
	h_of_t_object = Spline1D(
		data_w_object_spline_fit[:, "t [s]"], data_w_object_spline_fit[:, "h [cm]"]; k=4, s=20.0, bc="nearest"
	)
end

# â•”â•â•¡ f3f886d6-3010-4dd9-b42a-d5309463beb6
h_of_t_object(30.0)

# â•”â•â•¡ 5003d6c6-fa30-423e-80c1-a0f82e4085b9
derivative(h_of_t_object, 30.0) # WARNING: doesn't extrapolate.

# â•”â•â•¡ 36b1822e-fe08-494b-a57d-5888163a7b54
function viz_spline_fit(data_w_object::DataFrame, h_of_t_object::Spline1D, h_hole::Float64)
	fig = Figure(size=(500, 600))
	axs = [Axis(fig[i, 1]) for i = 1:2]
	linkxaxes!(axs...)
	axs[2].xlabel = "t [s]"
	axs[1].ylabel = "h(t) [cm]"
	axs[2].ylabel = "dh/dt [cm/s]"
	
	ts = range(0.0, data_w_object[end, "t [s]"], length=150)

	# top
	scatter!(axs[1], data_w_object[:, "t [s]"], data_w_object[:, "h [cm]"])
	hs = h_of_t_object.(ts)
	lines!(axs[1], ts, hs, color=Cycled(3))
	ylims!(axs[1], 0, nothing)

	# bottom
	hâ€² = [derivative(h_of_t_object, táµ¢) for táµ¢ in ts]
	# flatten derivative since it doesn't handle extrapolation
	hâ€²[hâ€² .> 0.0] .= 0.0
	# shut off derivative
	lines!(axs[2], ts, hâ€²)
	
	for ax in axs
		xlims!(ax, 0, nothing)
	end
	# save("block_data_spline_fit.png", fig)
	fig
end

# â•”â•â•¡ 29ccc89b-f76a-44fa-8a89-e3ca10742ba1
viz_spline_fit(data_w_object, h_of_t_object, tank_measurements.h_hole)

# â•”â•â•¡ 29d3cc8f-780b-449a-87ba-8d543ad2473b
function classical_soln_A_object(
	h_of_t_object::Spline1D,
	c_opt::Float64,
	tank_measurements::TankMeasurements
)
	ts = range(0.0, 1000.0, length=150)

	# dh/dt
	hâ€² = [derivative(h_of_t_object, táµ¢) for táµ¢ in ts]
	hâ€²[hâ€² .> 0.0] .= 0.0 # flatten to handle BC

	# h
	hs = h_of_t_object.(ts)

	# inferred area in tank
	A_tank = hs / tank_measurements.H * tank_measurements.A_t .+ 
		(1 .- hs / tank_measurements.H) * tank_measurements.A_b
	Aâ‚’ = A_tank .+ Ï€ * tank_measurements.r_hole ^ 2 * c_opt * 
			sqrt.(2 * g * (hs .- tank_measurements.h_hole)) ./ hâ€²
	
	inferred_object_data = DataFrame("h [cm]" => hs, "Aâ‚’ [cmÂ²]" => Aâ‚’)
	
	# filter data where (i) dh/dt
	filter!(row -> ! isinf(row["Aâ‚’ [cmÂ²]"]), inferred_object_data)
	
	return inferred_object_data
end

# â•”â•â•¡ 0a48e016-2fba-47cc-a212-47b4a3324b20
classical_Aâ‚’ = classical_soln_A_object(h_of_t_object, c_opt, tank_measurements)

# â•”â•â•¡ 5feb46c0-3888-4586-8b12-f990d4d38912
begin
	local fig = Figure()
	local ax = Axis(
		fig[1, 1], 
		xlabel="water level, h [cm]", 
		ylabel="cross-sectional area\nof object\nAâ‚’ [cmÂ²]"
	)
	
	lines!(
		classical_Aâ‚’[:, "h [cm]"], classical_Aâ‚’[:, "Aâ‚’ [cmÂ²]"], 
		label="predicted", color=Cycled(4)
	)
	scatter!(
		object_true_area[:, "h [cm]"], object_true_area[:, "area [cmÂ²]"], 
		label="measured"
	)
	
	xlims!(0, nothing)
	ylims!(-10, 100)
	hlines!(0.0, color="lightgray")
	axislegend()
	fig
end

# â•”â•â•¡ b23dc763-d91f-4d66-94d2-dcf96cb07f54
md"## Bayesian inference"

# â•”â•â•¡ 798d8d16-1c19-400d-8a94-e08c7f991e33
@model function infer_object_area(
	data_w_object::DataFrame, train_posterior::DataFrame, Î³::Float64; N=10
)
	#=
	yesterday's posterior is today's prior
	=#
	# variance for measuring length.
	Ïƒ_â„“ = mean(train_posterior.Ïƒ_â„“)
	Ïƒ = mean(train_posterior.Ïƒ)
	
	#=
	prior distributions
	=#
	# defines variance for measuring length.
	# height of tank
	H ~ Truncated(Normal(mean(train_posterior.H), std(train_posterior.H)), 
				mean(train_posterior.H) - 4 * std(train_posterior.H),
				mean(train_posterior.H) + 4 * std(train_posterior.H))

	# radius of the hole
	r_hole ~ Truncated(Normal(mean(train_posterior.r_hole), 	
					  std(train_posterior.r_hole)),
					  mean(train_posterior.r_hole) - 4 * std(train_posterior.r_hole),
					  mean(train_posterior.r_hole) + 4 * std(train_posterior.r_hole))
		

	# discharge coefficient. 
	c ~ Truncated(Normal(mean(train_posterior.c), std(train_posterior.c)),
				mean(train_posterior.c) - 2 * std(train_posterior.c),
				mean(train_posterior.c) + 2 * std(train_posterior.c))

	# height of the hole
	h_hole ~ Truncated(Normal(mean(train_posterior.h_hole), 
					  std(train_posterior.h_hole)),
					  mean(train_posterior.h_hole) - 4 * std(train_posterior.h_hole),
					  mean(train_posterior.h_hole) + 4 * std(train_posterior.h_hole))

	# initial liquid level
	hâ‚€_obs = data_w_object[1, "h [cm]"]
	hâ‚€ ~ Truncated(Normal(hâ‚€_obs, Ïƒ), hâ‚€_obs - 4 * Ïƒ, hâ‚€_obs + 4 * Ïƒ) # this should be removed but just trying to get the code to work first
		

	# bottom, top tank area measurements
	# std of product of two Guassians
	#   https://ccrma.stanford.edu/~jos/sasp/Product_Two_Gaussian_PDFs.html
	A_b ~ Truncated(Normal(mean(train_posterior.A_b), Ïƒ_â„“),
					mean(train_posterior.A_b) - 4 * std(train_posterior.A_b),
					mean(train_posterior.A_b) + 4 * std(train_posterior.A_b))
	
	A_t ~ Truncated(Normal(mean(train_posterior.A_t), Ïƒ_â„“),
					mean(train_posterior.A_t) - 4 * std(train_posterior.A_t),
					mean(train_posterior.A_t) + 4 * std(train_posterior.A_t))

	# Random distribution of obstacle area grid at points N 
	As ~ filldist(Normal(), N) # cmÂ²

	function A_of_tank(h)
		if h < 0
			# print(h)
			h = 0
			
		elseif h > H
			# print(h)
			h = H
			
		end
		return h / H * A_t + (1 - h / H) * A_b
	end

	# corresponding h's for the unknown A's
	hs = range(h_hole, hâ‚€_obs, length=N)
	# prior: object could be ANY size
	As[1] ~ Uniform(0.1, 0.9 * A_of_tank(hs[1]))
	for i in 2:N
		As[i] ~ Truncated(
						  As[i-1] + Î³ * Normal(0.0, 1.0), 
						  0.0, 
						  0.99 * A_of_tank(hs[i])
						 )
	end

	#=
	set up dynamic model for h(t)
	=#
	A_of_object = linear_interpolation(hs, As, 
					extrapolation_bc=Interpolations.Flat()) # Flat returns 0 outside the interpolation grid.

	# parameter for ODE solver
	params = (
			  r_hole=r_hole,
			  c=c,
			  h_hole=h_hole,
			  A_of_h=h ->  A_of_tank(h) - A_of_object(h)
			)
	
	# set up ODE
	tspan = (0.0, 4000)
	prob = ODEProblem(f, hâ‚€, tspan, params, saveat=1.0)
	h_of_t = solve(prob)
	
	# Observations.
	for i in 2:nrow(data)
		táµ¢ = data_w_object[i, "t [s]"]
		hÌ‚áµ¢ = h_of_t(táµ¢, continuity=:right)[1]
		data_w_object[i, "h [cm]"] ~ Normal(hÌ‚áµ¢, Ïƒ)
	end
	return nothing
end

# â•”â•â•¡ daa1e6c7-867b-4cf7-b7b8-dc24f859ec96
# â• â•â•¡ disabled = true
#=â• â•â•¡
data_w_object = downsample(all_data_w_object, 10)
  â• â•â•¡ =#

# â•”â•â•¡ 02939a87-e811-4ae4-8b6b-173370029889
begin
	Î³ = 5.0
	object_tank_model = infer_object_area(ds_block_data, train_posterior, Î³)
	object_posterior = sample(object_tank_model, NUTS(0.65), MCMCSerial(), n_MC_sample, 3; progress=true) 
				
	# )
end

# â•”â•â•¡ a127225a-5b79-4074-a16b-cecd11030800
function viz_area(posterior::DataFrame, data::DataFrame; N=10, 
				  Î³::Float64=Î³)
	fig = Figure()
	ax = Axis(fig[1, 1], xlabel="height [cm]", ylabel="Area [cmÂ²]")
	
	for j in 1:nrow(posterior)
		As = Vector{Float64}()
		[append!(As, posterior[j, "As[$(i)]"]) for i in 1:N]

		hs = range(posterior[j, "h_hole"], data[1, "h [cm]"], length=N) 
		lines!(hs, As, label="model", color=(:green, 0.1))
	end

	scatter!(obstacle_true_area[:, "h [cm]"], obstacle_true_area[:, "area [cmÂ²]"], 
			label="true_obstacle_area") 

	axislegend(unique=true, position=:rb)

	ax.title = "Î³=$Î³; smooth_grid=$N"
	save("area_inferred_3.png", fig)
	return fig
end

# â•”â•â•¡ b43f9f58-94fd-4c92-8e91-9a6b86cfc041
viz_area(object_posterior, test_infer)

# â•”â•â•¡ b4470c0e-191d-484c-be64-884b00b36580
ds_block_data

# â•”â•â•¡ 58f6b039-fec4-4ffd-b3e4-6b8145e05a8d
begin

end

# â•”â•â•¡ f98be3b6-bba8-4e85-8ebc-e1b568a44311


# â•”â•â•¡ aaa8f514-bf02-48ba-b384-353c9b58c794
begin
	local fig = Figure()
	local ax = Axis(
		fig[1, 1], 
		xlabel="water level, h [cm]", 
		ylabel="dh/dt [cm/s]"
	)
	
	lines!(ds_block_data[:, "h [cm]"], derivative(h_of_t, ds_block_data[:, "t [s]"]))
	fig
end

# â•”â•â•¡ afbf0413-406e-45a9-aead-a0cac98bec7a
derivative(h_of_t, ds_block_data[:, "t [s]"])

# â•”â•â•¡ 5fc47f1f-f04d-41b7-8126-d3ff619ceb01
ds_block_data

# â•”â•â•¡ b1e15e54-1f7b-4cea-b31f-9698aec480a4
block_data[:, "t [s]"]

# â•”â•â•¡ 6ee72380-9bcb-4c21-b979-f49d7fc98d1d
params

# â•”â•â•¡ b6dec8c5-0d06-4054-b985-6fd1789c7149
derivative(h_of_t, ds_block_data[:, "t [s]"])

# â•”â•â•¡ 7374ac4b-c2ac-4d6c-9bca-737f71dfb4f0
begin
	local fig = Figure()
	local ax = Axis(
		fig[1, 1], 
		xlabel="water level, h [cm]", 
		ylabel="Area of obstruction [cmÂ²]"
	)
	local ts = range(0.0, ds_block_data[end, "t [s]"], length=100)
	local hs = h_of_t.(ts)
	
	_A = Ï€ * params.r_hole ^ 2 * params.c * 
			sqrt.(2 * g * (hs .- params.h_hole)) ./ derivative(h_of_t, ts)
	
	lines!(hs, [A_of_h(h, tank_measurements) for h in hs] + _A)
	scatter!(object_true_area[:, "h [cm]"], object_true_area[:, "area [cmÂ²]"])
	
	xlims!(0, nothing)
	ylims!(0, 100)
	save("zoom_in.png", fig)
	
	fig
end

# â•”â•â•¡ 1a5770f2-4fcc-4023-b4cc-dfae5c0316dc
maximum(_A)

# â•”â•â•¡ f9e32388-d40e-48bc-ac91-c91bc914dbd7
begin
	local ts = range(0.0, ds_block_data[end, "t [s]"], length=100)
	local hs = h_of_t.(ts)
	maximum(Ï€ * params.r_hole ^ 2 * params.c * sqrt.(2 * g * (hs .- params.h_hole)))
	maximum(derivative(h_of_t, ts))
end

# â•”â•â•¡ Cell order:
# â• â•faf59350-8d67-11ee-0bdd-2510e986118b
# â• â•4391f124-cbef-46e5-8462-e4e5126f5b38
# â• â•245836a9-6b44-4639-9209-e7ad9035e293
# â•Ÿâ”€7752316d-9dd0-4403-aa08-22c977ff3727
# â• â•c976bc08-97b2-45c0-b1ff-1819e7290a68
# â• â•20fa4266-be80-4d8e-b1d0-155a40a1241f
# â• â•48d7273e-a48b-49fd-991b-6e29f64a0760
# â• â•9a7e5903-69be-4e0a-8514-3e05feedfed5
# â•Ÿâ”€418525b7-c358-41da-b865-5df3feb15855
# â• â•a95e371e-9319-4c7e-b5d9-4c4a50d12cd7
# â•Ÿâ”€6d48d04b-0786-4d15-a07b-8941805a5b09
# â• â•9af216b7-4bf2-42fb-bd95-5b2040d019a7
# â•Ÿâ”€6ebe0cb0-ba35-411c-9a7a-a8b6eecf326f
# â• â•385442da-f101-4ddf-8293-46d71d6a48fc
# â•Ÿâ”€ef43e50a-5af8-4733-88a4-cd159d173034
# â• â•9dabad13-cfa4-4e06-950d-f7c7d96c1147
# â•Ÿâ”€e040094c-7511-4831-b94a-1c1185868202
# â• â•23ee0e85-a84b-4b63-b432-5526559efcee
# â•Ÿâ”€078c01f7-e47e-4af0-be1c-ac4527b735fd
# â• â•8b00d2b3-9182-42ab-8393-91707b813f60
# â•Ÿâ”€7899f488-9c48-466f-857d-f5a31b5820ab
# â•Ÿâ”€96f26378-846c-4964-935c-0372e2e86e91
# â• â•2ddf387c-5a61-4490-9746-96e1589c7a74
# â• â•b2228d5c-16b4-4fee-b9b6-1112d7cf391c
# â• â•661feb84-339c-4bbe-a8a5-65de74ed58c8
# â• â•1e8a535e-25ea-490b-b545-e532c4fbc0f3
# â•Ÿâ”€a0849611-23b3-4a91-a054-f390bc6c9f0a
# â• â•33f889d7-e875-40d8-9d6d-cc87b0fbaf22
# â• â•0e4b3c36-2f09-405e-912b-22c893cd1715
# â• â•ddd8f749-3126-4563-8177-4941b6b0447b
# â•Ÿâ”€710ddf42-2397-4d96-9a61-ff5c600ccd43
# â• â•46084a31-e591-42f2-b8e6-02183ddfc6ac
# â• â•f21dc58e-d4e8-4314-b5dd-abbcb29efe86
# â• â•14b713b9-70c6-4506-a7b3-c67d021f8fce
# â• â•86b34265-6f2d-48d7-95ea-d10c8ae29ea9
# â• â•6bbb7a2c-464f-4e92-ab15-66745bac03ef
# â• â•bcf21d4b-cc76-4b9e-8cdb-7a17eb2af605
# â•Ÿâ”€e379461f-8896-4e2a-a71b-1871a8a37eb5
# â• â•7b7baa41-0185-4ed6-8fae-3a44e9912016
# â•Ÿâ”€f2f236f4-59f3-4c05-811d-078cd04ddd79
# â• â•c6a263eb-cb45-4ee7-9c02-549c89298652
# â• â•05ed4187-a01a-4a16-a0e7-b3867d252578
# â•Ÿâ”€6f7d4335-d9a6-4896-9d69-bfc1c2c1c3d0
# â• â•66815e8e-09d9-4b43-9f45-9379b3d34f78
# â•Ÿâ”€5e79d8e1-429c-414f-b3a6-8cf4b93d1336
# â• â•0b75c073-f167-4553-b746-539a14cfcf25
# â• â•4ed31219-9ce0-4f1b-8152-0002e64649ad
# â• â•0763f1f3-dfee-4d1f-a934-bf387f9c80ff
# â• â•c57c808a-297c-4887-bf20-5ad0207d055e
# â• â•d3307918-1fdb-4f87-bb92-67330d22e58b
# â•Ÿâ”€d25cda2f-6ec6-4c93-8860-f5ce9c3ee629
# â• â•444f6d74-273e-486d-905a-1443ec0e98df
# â• â•8cfdc784-4060-48b8-8d1a-3b8d11f7a9a7
# â•Ÿâ”€a1a10e2f-1b78-4b93-9295-7c0055e32692
# â• â•58eff13c-44b5-4f19-8a42-cf9907ac9515
# â•Ÿâ”€8a21fa0f-d3c3-4aa2-8b8b-74001d921c4a
# â• â•8f5b8859-6b8c-4f2a-af3a-b13c2d33fe2a
# â• â•8082559e-a5b0-41a8-b8ed-aec3b09e5b2b
# â•Ÿâ”€2ee1ca40-141f-40ad-b4c1-a2e025f69f95
# â• â•c2d877b5-d309-4868-925d-dab8d7d23403
# â•Ÿâ”€c239deed-8291-45aa-95cf-94df26e0136d
# â• â•ccb1f005-567d-47f8-bec1-8db268d878ec
# â• â•5bb0b72a-8c77-4fcb-bbde-d144986d9c1e
# â• â•ded5b462-06dd-43a4-93b0-c52ad87174eb
# â•Ÿâ”€86b56683-c80e-4c0f-8b03-a4869860d04f
# â• â•2ab35999-3615-4f5c-8d89-36d77802fe9b
# â• â•2a01b228-f281-46c4-9764-fac6cc1b4217
# â•Ÿâ”€a5ae695b-bfc0-4425-9b64-bbeeba7da015
# â• â•eaf470e9-2898-41d5-a6d5-4cd846e9c0de
# â• â•a3ba0c9d-5f81-4023-9ce0-ff29536aa968
# â•Ÿâ”€9533c662-80af-4dd4-bf25-02e894867360
# â• â•b06a1c07-6250-4324-8802-010e5d847edb
# â• â•8b6d766a-8f7b-4b9a-9a15-0f7375087120
# â• â•807222ba-5ff8-4f33-a9a0-7c69b1dccf52
# â• â•16158266-36ed-44c3-a418-0c454955ce78
# â•Ÿâ”€580de17a-625d-420e-974c-86766197025e
# â• â•cb59f55b-c748-4a94-b344-e50a8fa7c690
# â• â•89cced40-f24e-499e-8bfd-19c3964f689b
# â• â•b9515b3a-b254-49ae-8c2c-b8ce7ced4d3a
# â•Ÿâ”€a8861082-2214-45f1-bc49-733efe74c949
# â• â•b59fa654-6946-4687-b14b-c2ef1f766f5c
# â• â•b12963ae-bf7d-4ef7-b1a8-e2d1e24f9b4b
# â• â•cfbe753d-85a8-445f-9eda-14a376d7e0c6
# â•Ÿâ”€c53edeef-324a-418f-907d-aaf557cb8d24
# â• â•4831a3be-35d3-420c-8463-bb14a597cc6a
# â• â•f3f886d6-3010-4dd9-b42a-d5309463beb6
# â• â•5003d6c6-fa30-423e-80c1-a0f82e4085b9
# â• â•36b1822e-fe08-494b-a57d-5888163a7b54
# â• â•29ccc89b-f76a-44fa-8a89-e3ca10742ba1
# â• â•29d3cc8f-780b-449a-87ba-8d543ad2473b
# â• â•0a48e016-2fba-47cc-a212-47b4a3324b20
# â• â•5feb46c0-3888-4586-8b12-f990d4d38912
# â•Ÿâ”€b23dc763-d91f-4d66-94d2-dcf96cb07f54
# â• â•798d8d16-1c19-400d-8a94-e08c7f991e33
# â• â•daa1e6c7-867b-4cf7-b7b8-dc24f859ec96
# â• â•02939a87-e811-4ae4-8b6b-173370029889
# â• â•a127225a-5b79-4074-a16b-cecd11030800
# â• â•b43f9f58-94fd-4c92-8e91-9a6b86cfc041
# â• â•b4470c0e-191d-484c-be64-884b00b36580
# â• â•58f6b039-fec4-4ffd-b3e4-6b8145e05a8d
# â• â•f98be3b6-bba8-4e85-8ebc-e1b568a44311
# â• â•aaa8f514-bf02-48ba-b384-353c9b58c794
# â• â•afbf0413-406e-45a9-aead-a0cac98bec7a
# â• â•5fc47f1f-f04d-41b7-8126-d3ff619ceb01
# â• â•b1e15e54-1f7b-4cea-b31f-9698aec480a4
# â• â•6ee72380-9bcb-4c21-b979-f49d7fc98d1d
# â• â•b6dec8c5-0d06-4054-b985-6fd1789c7149
# â• â•7374ac4b-c2ac-4d6c-9bca-737f71dfb4f0
# â• â•1a5770f2-4fcc-4023-b4cc-dfae5c0316dc
# â• â•f9e32388-d40e-48bc-ac91-c91bc914dbd7
